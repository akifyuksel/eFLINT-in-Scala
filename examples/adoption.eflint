Fact person Identified by String

Placeholder parent For person
Placeholder child For person

Fact legal-parent Identified by parent * child
    Holds when adoptive-parent(parent, child)
            || natural-parent (parent, child)

Fact natural-parent Identified by parent * child
Fact adoptive-parent Identified by parent * child

Fact is-adopted Identified by child

Fact adoptions Identified by Int
    Derived from Count(Foreach child : child When Holds is-adopted(child))

Fact eligible Identified by parent

Fact authority

Act sign-adoption-papers
    Actor authority
    Recipient parent
    Related to child
    Creates duty-to-stay-eligible(parent, authority, child), adoptive-parent(parent, child), is-adopted(child)
    Holds when eligible(parent)

Duty duty-to-stay-eligible
    Holder parent
    Claimant authority
    Related to child
    Violated when !eligible(parent)

Act neglect
    Actor parent
    Recipient child
    Terminates eligible(parent), adoptive-parent(parent,child), is-adopted(child)
    Holds when legal-parent(parent, child)
.

+person(Alice).
+person(Bob).
+eligible(Alice).
sign-adoption-papers(Eve, Alice, Bob).
neglect(Alice, Bob).
